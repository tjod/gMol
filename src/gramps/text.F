C *************************************************************
C This file contains source code for the gMol computer program
C  Copyright (C) 1981-2010 by TJ O'Donnell and
C  Copyright (C) 2005-2010 gNova, Inc.
C It is unlawful to modify or remove this copyright notice.
C See the accompanying LICENSE file for further information. 
C *************************************************************
	SUBROUTINE TEXT(ARG,SWITCHES)
C
	IMPLICIT NONE
	CHARACTER ARG*(*)
	CHARACTER SWITCHES*(*)
C
#include "UDLIST.FOR"
#include "ERRORS.FOR"
C
	EXTERNAL TRIM
	CHARACTER*80 CHLINE
	CHARACTER*(NAMMAX) NAME
	CHARACTER*1 BLANK
	INTEGER EIGHT, ZERO, VPOINT, CHLEN, I
	LOGICAL PRESENT
	REAL XMOVE, YMOVE, ZMOVE, WMOVE
C
	INTEGER G0ITSIZE,G0IXTSCALE,G0IYTSCALE
	CHARACTER*100                            G0IFONT
	EXTERNAL  G0ITSIZE,G0IXTSCALE,G0IYTSCALE,G0IFONT
	CHARACTER*100 TMPFONT
C
	DATA BLANK/' '/
	DATA ZERO/0/,EIGHT/8/
	BLANK=' '
	ZERO=0
	EIGHT=8
C
C	THIS ROUTINE CREATES AN OBJECT WHICH HAS TEXT DATA
C	IN IT.  COORDINATES FOR THE TEXT ARE OPTIONALLY GIVEN.
C
C	 GET THE CHARACTER STRING AND THEN
C	 GET THE NAME. CHECK TO SEE IF
C	 THIS NAME IS ALREADY IN USE.  
C
	POINT=1
	CALL GETSTR(ARG,POINT,CHLINE)
	IF(ERROR.NE.0)CALL TELLUSER(ERROR,*999)
	IF(NPOINT.EQ.0)CALL TELLUSER(INCOMP,*999)
	IF(POINT.EQ.0)CALL TELLUSER(NONAME,*999)
	VPOINT=POINT
	CALL NAMEID(ARG,POINT,ID,NAME)
	IF(ERROR.EQ.0)ERROR=DUPNAM
	IF(ERROR.NE.OBJERR)THEN
	  POINT=VPOINT
	  CALL TELLUSER(ERROR,*999)
	END IF
	ERROR=0
	POINT=NPOINT
C
C	 FIND THE LENGTH OF THE TEXT OMITTING TRAILING BLANKS
C
	CHLEN=TRIM(CHLINE)
C
C	 SET DEFAULT POSITION VALUES AND TRY TO READ USER VALUES.
C	 IF THE /FLOAT SWITCH IS USED, DON'T BOTHER
C
	IF(.NOT.MATCH(SWITCHES,'FLOAT'))THEN
	  CALL TEXTXYZ(ARG,POINT,CHLEN,G0ITSIZE(),XMOVE,YMOVE,
     1	                 ZMOVE,WMOVE)
	  IF(ERROR.NE.0)THEN
	    CALL TELLUSER(ERROR,*999)
	  END IF
	  IF(MPSERR.NE.0)GOTO 998
	END IF
C
C	 OPEN THE OBJECT IN THE UNTRANSFORMED DISPLAY LIST
C	 AND MARK IT AS CONTAINING TEXT.
C
	CALL OPENOBJ(NAME,ORDINARY)
	IF(ERROR.NE.0)CALL TELLUSER(ERROR,*999)
	KINDOBJ(NOBJ)=IOR(KINDOBJ(NOBJ),TEXTBYTE)
C
C	 FILL IN THE DISPLAY LIST WITH TEXT INFO LINE, FOLLOWED
C	 BY X,Y,Z TEXT POSITION.
C
	DLIST(UDFREE)=ZERO
C
C	 TEXT USES FORMAT FOR SOFT TEXT, THOUGH FONT=BLANK CAN
C	 MEAN USE HARD TEXT.  USER HAS CHOICE, BUT INTERNAL FORMAT
C	 WILL ALWAYS LEAVE ROOM FOR FONT, HENCE LDM2=8.
C
	DLIST(UDFREE+1)=EIGHT
C
	DLIST(UDFREE+2)=G0ITSIZE()
	UDFREE=UDFREE+3
	LENGTH=LENGTH+3
	DLIST(UDFREE)  =XMOVE
	DLIST(UDFREE+1)=YMOVE
	DLIST(UDFREE+2)=ZMOVE
C***************************************
C*******TEXT IS ALWAYS FOUR DIMENSIONAL*
	DLIST(UDFREE+3)=WMOVE
	UDFREE=UDFREE+4
	LENGTH=LENGTH+4
C***************************************
C
C	 PUT IN SCALE AND FONT TYPE
C
	DLIST(UDFREE)   = G0IXTSCALE()
	DLIST(UDFREE+1) = G0IYTSCALE()
	TMPFONT=G0IFONT()
	DO 10 I=1,4
	DLIST(UDFREE+1+I)=ICHAR(TMPFONT(I:I))
 10   CONTINUE
	UDFREE=UDFREE+6
	LENGTH=LENGTH+6
C
C	 PUT THE TEXT LENGTH AND STRING INTO THE DISPLAY LIST,
C	 AND IGNORE TRAILING BLANKS IN THE STRING.
C
	DLIST(UDFREE)=CHLEN
	UDFREE=UDFREE+1
	LENGTH=LENGTH+1 
	DO 100 I=1,CHLEN
	DLIST(UDFREE+I-1)=ICHAR(CHLINE(I:I))
 100  CONTINUE
	UDFREE=UDFREE+CHLEN
	LENGTH=LENGTH+CHLEN
C
 998  CALL CLOSEOBJ(ORDINARY)
C
 999  CONTINUE
	RETURN
C
	END
