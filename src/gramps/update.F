C *************************************************************
C This file contains source code for the gMol computer program
C  Copyright (C) 1981-2010 by TJ O'Donnell and
C  Copyright (C) 2005-2010 gNova, Inc.
C It is unlawful to modify or remove this copyright notice.
C See the accompanying LICENSE file for further information. 
C *************************************************************
      INTEGER FUNCTION UPDATE(TYPE,WHICH,VALL,REL)
C
      IMPLICIT NONE
      INTEGER TYPE, WHICH
	REAL VALL, REL
C
C#include "DEVICES.FOR"
#include "UDLIST.FOR"
	INTEGER QWHICH, I, J
C
C       THIS SUBROUTINE TAKES VALUES FROM THE DEVICE SERVICE
C       SUBROUTINE AND DISTRIBUTES THEM TO THE CORRECT PLACES IN
C       THE TFMOBJ ARRAY.  BIT MASKS TELL WHICH TRANSFORMATIONS
C       ARE TIED TO WHICH DEVICES.
C
C       DEVUSE RECORDS DEVICE USAGE FOR EACH OBJECT
C       BITS ARE SET FOR UP TO 32 DEVICES
C
C       FNUSE RECORDS FUNCTION USAGE FOR EACH OBJECT
C       BITS ARE SET FOR UP TO 32 FUNCTIONS
C
C       TFMVAL RECORDS THE VALUE TO USE FOR THE TRANSFORMATION
C       REPRESENTING AN ANGLE, SCALE FACTOR, ETC.
C
C       TFMINC RECORDS INCREMENTAL VALUES FOR CONTINUOUS UPDATES
C       OR THE PARAMETER FOR A FUNCTION.
C
C       TFMVAL IS ALSO CHANGED, IF NECESSARY BY CONTUPD.
C
C       FIRST, WE SET UP THE BIT MASK FOR THE DEVICE(S) WHICH HAVE JUST
C       BEEN UPDATED.  DIALS ARE IN BITS #0-7.  MOUSE ARE BITS #8-15.
C
C       SO SET UP THESE BITS AND SHIFT WHERE NECESSARY
C
      QWHICH=ISHFT(ABS(TYPE),WHICH-1)
C
C       NOW LOOK AT EACH OBJECT AND SEE IF IT'S TRANSFORMATIONS '
C       NEED UPDATING.  THE 'SUMMARY' WORD OF DEVUSE HAS THE INFORMATION
C       ABOUT DEVICES ASSIGNED TO ANY TYPE OF TRANSFORMATION
C       SO AS A QUICK CHECK SEE IF THE DEVICE WHICH JUST CHANGED ITS
C       VALUE IS EVEN USED BY THE OBJECT IN ANY TRANSFORMATION.
C
      DO 20 I=1,NOBJ
      IF(IAND(QWHICH,DEVUSE(SUMMARY,I)).EQ.0)GOTO 20
C
C       NOW CHECK THROUGH EACH POSSIBLE TRANSFORMATION AND UPDATE
C       THOSE VALUES WHICH NEED TO BE UPDATED.
C
C
      DO  10 J=1,MTFM
      IF( IAND(QWHICH,DEVUSE(J,I)).EQ.0)GOTO 10
C
C       CHECK FOR A POSSIBLE FUNCTION OF THIS DEVICE
C       ZERO MEANS NO SPECIAL FUNCTION
C
      IF(FNUSE(J,I).EQ.0)THEN
C
C       DIAL VALUES ARE UPDATED BY ADDING THE RELATIVE
C       CHANGE, WHILE PEN VALUES USE ABSOLUTE VALUES
C       OF THESE DEVICES (SEEMS MORE NATURAL THAT WAY).
C
        IF( TYPE.EQ.MOUSS) THEN
          TFMVAL(J,I)=TFMVAL(J,I) + REL
        ELSE
          TFMVAL(J,I)=VALL
        END IF
C       CALL USHOW(I,J)
        DCHANGE=.TRUE.
C
      ELSE
        CALL FNUPDATE(I,J,TYPE,VALL,REL)
        DCHANGE=.TRUE.
      END IF
C
C
 10   CONTINUE
C
C       DO THE SAME FOR THE EXTRA TRANSFORMATIONS OF THE WORLDS
C
      IF(I.GT.RWORLD)GOTO 20
      DO  15 J=1,WTFM
      IF( IAND(QWHICH,DEVWLD(J,I)).EQ.0)GOTO 15
C
      IF(FNWLD(J,I).EQ.0)THEN
        IF( TYPE.EQ.MOUSS) Then
          WLDVAL(J,I)=WLDVAL(J,I) + REL
        ELSE
          WLDVAL(J,I)=VALL
        END IF
C       CALL WSHOW(I,J)
        DCHANGE=.TRUE.
      ELSE
        CALL FNUPDWLD(I,J,TYPE,VALL,REL)
        DCHANGE=.TRUE.
      END IF
 15   CONTINUE
C
 20   CONTINUE
C
      if (dchange) then
       update = 1
      else
       update = 0
      end if
      RETURN
      END
