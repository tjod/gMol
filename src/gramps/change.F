C *************************************************************
C This file contains source code for the gMol computer program
C  Copyright (C) 1981-2010 by TJ O'Donnell and
C  Copyright (C) 2005-2010 gNova, Inc.
C It is unlawful to modify or remove this copyright notice.
C See the accompanying LICENSE file for further information. 
C *************************************************************
      SUBROUTINE CHANGE(ARG,SWITCHES)
C
      IMPLICIT NONE
C
#include "UDLIST.FOR"
#include "ERRORS.FOR"
      INTEGER SIZE
      INTEGER FROMDEV, TODEV, START, FINISH, DEPTH, LEVEL, I, K
C
      CHARACTER ARG*(*)
      CHARACTER SWITCHES*(*)
C
C       THIS ROUTINE CHANGES DEVICE ASSIGNMENT OF AN OBJ
C       BY ALTERING THE BIT MASKS IN DEVUSE SO THAT THE
C       BITS APPLYING TO ONE DEV ARE REMOVED AND THE BITS
C       APPLYING TO THE OTHER DEV ARE INSERTED
C
C       GET THE OBJ NAME AND TWO DEVS
C
      POINT=1
      CALL NAMID(ARG,POINT,ID)
      IF(ERROR.NE.0)CALL TELLUSER(ERROR,*999)
      CALL GETDEV(ARG,POINT,FROMDEV)
      IF(ERROR.NE.0)CALL TELLUSER(ERROR,*999)
      CALL GETDEV(ARG,POINT,TODEV)
      IF(ERROR.NE.0)CALL TELLUSER(ERROR,*999)
C
C       ALTER DOES THE DIRTY WORK
C
      CALL ALTER(ID,FROMDEV,TODEV)
C
C       IF /GROUP IS SPECIFIED, THEN EACH MEMBER OF
C       THE  GROUP IS AFFECTED.
C
      IF( (TYPOBJ(ID).EQ.GROUP).AND.MATCH(SWITCHES,'GROUP') )THEN
        CALL GROUPINFO(ID,START,FINISH,DEPTH,LEVEL,SIZE)
        DO 40 I=START+1,FINISH
        K=TREE(I)
        IF(K.GT.GROUPEND)CALL ALTER(K,FROMDEV,TODEV)
 40     CONTINUE
      END IF
C
 999  RETURN
      END
C
      SUBROUTINE ALTER(NID,FROMDEV,TODEV)
C
      IMPLICIT NONE
      INTEGER K, NID, FROMDEV, TODEV, I
C
#include "UDLIST.FOR"
C
C       IEOR EXCLUDES THE BITS IN THE FROMDEV BIT MASK
C       IOR INCLUDES THE BITS IN THE TODEV BIT MASK
C
C
      IF(IAND(DEVUSE(SUMMARY,NID),FROMDEV).EQ.0)RETURN
C
      DEVUSE(SUMMARY,NID)=IEOR(DEVUSE(SUMMARY,NID),FROMDEV)
      DEVUSE(SUMMARY,NID)=IOR(DEVUSE(SUMMARY,NID),TODEV)
      DO 10 I=1,MTFM
      IF(IAND(DEVUSE(I,NID),FROMDEV).NE.0)THEN
        DEVUSE(I,NID)=IEOR(DEVUSE(I,NID),FROMDEV)
        DEVUSE(I,NID)=IOR(DEVUSE(I,NID),TODEV)
        call setdevval(TODEV,TFMVAL(I,NID),1)
      END IF
 10   CONTINUE
C
C       IF ITS A WORLD, DO THE EXTRA WORLD TFMS, TOO.
C
      IF(NID.GT.RWORLD)RETURN
C
      DEVWLD(WSUMMRY,NID)=IEOR(DEVWLD(WSUMMRY,NID),FROMDEV)
      DEVWLD(WSUMMRY,NID)=IOR (DEVWLD(WSUMMRY,NID),TODEV)
      DO 20 I=1,WTFM
      IF(IAND(DEVWLD(I,NID),FROMDEV).NE.0)THEN
        DEVWLD(I,NID)=IEOR(DEVWLD(I,NID),FROMDEV)
        DEVWLD(I,NID)=IOR (DEVWLD(I,NID),TODEV)
        call setdevval(TODEV,WLDVAL(I,NID),1)
      END IF
 20   CONTINUE
C
      RETURN
      END
